spending validator

struct Datum {
    adaAmount: Int
    lcAmount: Int

    func get_ratio(self) -> Int {
        self.adaAmount / self.lcAmount
    }
}

enum Redeemer {
    AddAda
    Mint{ 
        pkhBA: ByteArray    // Destination address for littercoin
    }
    Burn { 
        pkhBA: ByteArray    // Return address for merchant token
    }
}

const LC_FIXED_SUPPLY : Int = 1000000000  // 1 Billion littercoin 
const minAda : Int = 2000000
const minAdaVal: Value = Value::lovelace(minAda)

// Define thread token value
const TT_MPH: ByteArray = #d335ad8888fc4091f373d19db206a66563e851fa5e8ee83c2decc2cf
const ttMph: MintingPolicyHash = MintingPolicyHash::new(TT_MPH)
const ttAssetclass: AssetClass = AssetClass::new(
        ttMph, 
        "Thread Token Littercoin".encode_utf8()
    )
const ttVal : Value = Value::new(ttAssetclass, 1)

// Define the mph of the littercoin asset class
//const LC_MPH: ByteArray = #5bea03876b6b618800348d23ca1bf9685488dea39b1979da3b6cf5e9
//const lcMph: MintingPolicyHash = MintingPolicyHash::new(LC_MPH)
const lcAssetClass: AssetClass = AssetClass::new(
        ttMph, 
        "Littercoin".encode_utf8()
    )

// Define the merchant token
const MERCHANT_MPH: ByteArray = #b63f671dcfbb0daaaf081f3b339243c7a533637d9755e5387a232a67
const merchMph: MintingPolicyHash = MintingPolicyHash::new(MERCHANT_MPH)
const merchAssetclass: AssetClass = AssetClass::new(
        merchMph, 
        "Merchant Token Littercoin".encode_utf8()
    )
const merchVal: Value = Value::new(merchAssetclass, 1)

// Define the pkh of the owner
const OWNER_PKH: ByteArray = #b9abcf6867519e28042048aa11207214a52e6d5d3288b752d1c27682 
const ownerPkh: PubKeyHash = PubKeyHash::new(OWNER_PKH)


func getDatum (txOuts : []TxOutput) -> []TxOutput {

    if (txOuts.is_empty()) {
        txOuts
    } else {
            txOuts.head.datum.switch {
            Inline => { []TxOutput::new_const(1, txOuts.head) },
            else => getDatum (txOuts.tail)
            }
    }
}


func main(datum: Datum, redeemer: Redeemer, ctx: ScriptContext) -> Bool {
    
    // Get hash of this validator
    vHash : ValidatorHash = ctx.get_current_validator_hash();
    tx : Tx = ctx.tx;
    txOutputs : []TxOutput = tx.outputs_locked_by(vHash);
    txOut : []TxOutput = getDatum(txOutputs);

    redeemer.switch {
        AddAda => {
            if (!txOut.is_empty()) {
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        addAdaDatumAmt: Int = outDat.adaAmount - datum.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);
                        lcResAmt: Int = LC_FIXED_SUPPLY - outDat.lcAmount;
                        lcResVal: Value = Value::new(lcAssetClass, lcResAmt);

                        (tx.value_locked_by(vHash) == (ttVal + adaVal + lcResVal)).trace("LCV1: ") &&
                        (addAdaDatumAmt >= minAda).trace("LCV2: ") &&
                        (outDat.lcAmount == datum.lcAmount).trace("LCV3: ")
                    },
                    else => false.trace("LCV3: ") // No inline datum found
                }
            } else {
                false.trace("LCV4: ") // No datum found in outputs
            }
        },
        red: Mint => {
             if (!txOut.is_empty()) {
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        //adaDatumAmt: Int = outDat.adaAmount - datum.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);
                        lcResAmt: Int = LC_FIXED_SUPPLY - outDat.lcAmount;
                        lcResVal:  Value = Value::new(lcAssetClass, lcResAmt);
                        lcDatumAmt: Int = outDat.lcAmount - datum.lcAmount;
                        lcMintVal: Value = Value::new(lcAssetClass, lcDatumAmt);
                        destPkh: PubKeyHash = PubKeyHash::new(red.pkhBA);
                        destOutTxs : []TxOutput = tx.outputs_sent_to(destPkh);

                        tx.is_signed_by(ownerPkh).trace("LCV5: ") &&  
                        (outDat.adaAmount == datum.adaAmount).trace("LCV6: ") && 
                        (tx.value_locked_by(vHash) == (adaVal + ttVal + lcResVal)).trace("LCV7: ") &&
                        //(destOutTxs.head.value.contains(minAdaVal + lcMintVal)).trace("LCV8: ") &&
                        (destOutTxs.head.value == (minAdaVal + lcMintVal)).trace("LCV8: ") &&
                        //(tx.value_sent_to(destPkh) == (minAdaVal + lcMintVal)).trace("LCV8: ") &&
                        (0 < lcDatumAmt && lcDatumAmt < outDat.adaAmount).trace("LCV9: ") 
                    },
                    else => false.trace("LCV10: " ) // Non inline datum found
                }
             } else {
                false.trace("LCV11: ") // No datum found in outputs
             }
        },
        red: Burn => {
             if (!txOut.is_empty()) {    
                txOut.head.datum.switch {
                    dat: Inline => { 
                        outDat: Datum = Datum::from_data(dat.data);
                        adaDatumAmt: Int = datum.adaAmount - outDat.adaAmount;
                        adaVal: Value = Value::lovelace(outDat.adaAmount);
                        lcResAmt: Int = LC_FIXED_SUPPLY - outDat.lcAmount;
                        lcResVal:  Value = Value::new(lcAssetClass, lcResAmt);
                        lcDatumAmt: Int = datum.lcAmount - outDat.lcAmount;
                        //lcBurnVal: Value = Value::new(lcAssetClass, lcDatumAmt) * (-1);
                        ratio: Int = datum.get_ratio();
                        adaWithdraw : Int = lcDatumAmt * ratio;
                        adaWithdrawVal: Value = Value::lovelace(adaWithdraw);
                        merchPkh: PubKeyHash = PubKeyHash::new(red.pkhBA);
                        merchOutTxs : []TxOutput = tx.outputs_sent_to(merchPkh);
                                         
                        (adaDatumAmt == adaWithdraw).trace("LCV12: ") &&
                        (tx.value_locked_by(vHash) == (ttVal + adaVal + lcResVal)).trace("LCV13: ") &&
                        //tx.minted.contains(lcBurnVal).trace("LCV14: ") &&
                        //(tx.value_sent_to(merchPkh) == (minAdaVal + merchVal)).trace("LCV14: ") &&
                        //(merchOutTxs.head.value.contains(adaWithdrawVal + merchVal)).trace("LCV15: ")
                        (merchOutTxs.head.value == (adaWithdrawVal + merchVal)).trace("LCV15: ")
                        //merchOutTxs.get(1).value.contains(adaWithdrawVal).trace("LCV15: ") &&
                        //merchOutTxs.get(2).value.contains(minAdaVal + merchVal).trace("LCV16: ") 
                    },
                    else => false.trace("LCV15: ") // No inline datum found
                }
            } else {
                false.trace("LCV16: ") // No datum found in outputs
            }
        }
    }
}

const LC_DATUM = Datum {
    lcAmount : 0,
    adaAmount : 0
}

const VAL_ADD_ADA_REDEEMER = Redeemer::AddAda
const VAL_MINT_REDEEMER = Redeemer::Mint{#1a2b3c}
const VAL_BURN_REDEEMER = Redeemer::Burn{#1a2b3c}

